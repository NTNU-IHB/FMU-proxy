
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.10" apply false //apply in sub-projects
}

println "Gradle version is ${GradleVersion.current().version}"

ext.fmi4j_snapshot = false
ext.fmi4j_version = fmi4j_snapshot ? 'dev-SNAPSHOT' : '0.12.2'

ext.grpc_version = '1.16.1'
ext.yajrpc_version = '0.10.0'
ext.slf4j_version = '1.7.25'

subprojects { sub ->

    apply plugin: 'java'

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {

        sub.plugins.withId('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            implementation "org.jetbrains.kotlin:kotlin-reflect"

        }

        def junit_version = '5.3.1'
        testImplementation("org.junit.jupiter:junit-jupiter-api:${junit_version}")
        testImplementation("org.junit.jupiter:junit-jupiter-params:${junit_version}")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junit_version}")
		
    }

    test {
        useJUnitPlatform()
    }
    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
                
                //required for @JvmDefault
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }
    }

}

