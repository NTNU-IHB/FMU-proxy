
plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.21" apply false //apply in sub-projects
}

println "Gradle version is ${gradle.getGradleVersion()}"

ext.fmi4j_snapshot = false
ext.fmi4j_version = fmi4j_snapshot ? 'master-SNAPSHOT' : '0.17.0'

ext.grpc_version = '1.18.0'
ext.slf4j_version = '1.7.25'

subprojects { sub ->

    apply plugin: "java"

    group = "no.ntnu.ihb.fmuproxy"

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    dependencies {

        sub.plugins.withId('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
            implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.3.21'
        }
        
    }

    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
                
                //required for @JvmDefault
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }
    }

}

