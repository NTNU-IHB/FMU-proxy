plugins {
    id "base"
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.4.0" apply false //apply in sub-projects
}

println "Gradle version is ${gradle.getGradleVersion()}"

ext.grpc_version = '1.25.0'
ext.slf4j_version = '1.7.25'
ext.fmi4j_version = '0.31.4'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.6.1'
}

subprojects { sub ->

    version = "0.3.0"
    group = "no.ntnu.ihb.fmuproxy"

    repositories {
        mavenCentral()
        maven { url  "https://dl.bintray.com/ntnu-ihb/mvn" }
    }

    dependencies {

        sub.plugins.withId('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        }
        
    }
    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
                
                //required for @JvmDefault
                freeCompilerArgs = ['-Xjvm-default=enable']
            }
        }

        compileTestKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }
    }

}


tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp && name.startsWith("fmi4j_")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
