plugins {
    id "base"
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.4.10" apply false //apply in sub-projects
}

println "Gradle version is ${gradle.getGradleVersion()}"

ext.slf4j_version = '1.7.25'
ext.picocli_version = '4.5.0'
ext.fmi4j_version = '0.33.2'

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.6.1'
}

subprojects { sub ->

    repositories {
        mavenCentral()
        maven { url  "https://dl.bintray.com/ntnu-ihb/mvn" }
    }

    dependencies {

        sub.plugins.withId('kotlin') {
            implementation "org.jetbrains.kotlin:kotlin-stdlib"
        }
        
    }
    
    sub.plugins.withId('kotlin') {
        compileKotlin {
            kotlinOptions {
                jvmTarget = "1.8"
                javaParameters = true
            }
        }

        compileTestKotlin {
            kotlinOptions.jvmTarget = "1.8"
        }
    }

}


tasks.create('cleanTemp') {

    doLast {
        def temp = new File(System.getProperty("java.io.tmpdir"))
        temp.listFiles(new FilenameFilter() {
            @Override
            boolean accept(File dir, String name) {
                return dir == temp && name.startsWith("fmi4j_")
            }
        }).each {
            it.deleteDir()
        }
    }

}

clean.dependsOn(cleanTemp)
